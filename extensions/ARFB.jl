using ForneyLab
using LinearAlgebra
import ForneyLab: SoftFactor, @ensureVariables, generateId, addNode!, associate!
import ForneyLab: unsafeMeanCov, unsafeCov, unsafeMean, unsafePrecision, Multivariate, MatrixVariate


"""
Description:

    An element-wise multiplication with unknown process noise.
    When modelling Fourier coefficients, this is also known as a probabilistic phase vocoder:

    f(y, x, Œ∏, w) = ùí©(y | Œ∏ x, inv(w))

Interfaces:
    1. y (output vector)
    2. x (input vector)
    3. Œ∏ (autoregression coefficients)
    4. w (precision matrix)

Construction:
    AutoregressiveFilterbank(out, Œ∏, in, Œ≥, id=:some_id)

"""

mutable struct AutoregressiveFilterbank <: SoftFactor
    id::Symbol
    interfaces::Vector{Interface}
    i::Dict{Symbol,Interface}

    function AutoregressiveFilterbank(y, x, Œ∏, w; id=generateId(AutoregressiveFilterbank))
        
        # ensure that the input arguments are random variables
        @ensureVariables(y, x, Œ∏, w) 
        
        # create new object
        self = new(id, Array{Interface}(undef, 4), Dict{Symbol,Interface}())
        
        # add the node to the current factor graph
        addNode!(currentGraph(), self)
        
        # add argument variables to interfaces of node
        self.i[:y] = self.interfaces[1] = associate!(Interface(self), y)
        self.i[:x] = self.interfaces[2] = associate!(Interface(self), x)
        self.i[:Œ∏] = self.interfaces[3] = associate!(Interface(self), Œ∏)
        self.i[:w] = self.interfaces[4] = associate!(Interface(self), w)
        
        # return object
        return self
    end
end

# add shortcut for calling the filter bank
slug(::Type{AutoregressiveFilterbank}) = "ARFB"




function ruleVariationalARFBOutNPPP(marg_y::Nothing, 
                                    marg_x::ProbabilityDistribution{Multivariate}, 
                                    marg_Œ∏::ProbabilityDistribution{Multivariate}, 
                                    marg_w::ProbabilityDistribution{MatrixVariate})

    # calculate required means
    mŒ∏ = unsafeMean(marg_Œ∏)
    mx = unsafeMean(marg_x)
    mw = unsafeMean(marg_w)
                        
    # calculate new parameters
    my = mŒ∏ .* mx
    wy = mw

    # create variational message
    return Message(Multivariate, GaussianWeightedMeanPrecision, xi=wy*my, w=wy)

end


function ruleVariationalARFBIn1PNPP(marg_y::ProbabilityDistribution{Multivariate}, 
                                    marg_x::Nothing, 
                                    marg_Œ∏::ProbabilityDistribution{Multivariate}, 
                                    marg_w::ProbabilityDistribution{MatrixVariate})
    
    # caluclate required means
    my = unsafeMean(marg_y)
    mŒ∏ = unsafeMean(marg_Œ∏)
    mw = unsafeMean(marg_w)

    # calculate required variances
    vŒ∏ = unsafeCov(marg_Œ∏)

    # calculate new parameters
    wx = (vŒ∏' + mŒ∏*mŒ∏') .* mw
    mx = inv(wx) * Diagonal(mŒ∏) * mw * my

    # create variational message
    return Message(Multivariate, GaussianWeightedMeanPrecision, xi=wx*mx, w=wx)

end


function ruleVariationalARFBIn2PPNP(marg_y::ProbabilityDistribution{Multivariate}, 
                                    marg_x::ProbabilityDistribution{Multivariate}, 
                                    marg_Œ∏::Nothing, 
                                    marg_w::ProbabilityDistribution{MatrixVariate})

    # calculate required means
    my = unsafeMean(marg_y)
    mx = unsafeMean(marg_x)
    mw = unsafeMean(marg_w)

    # calculate required variances
    vx = unsafeCov(marg_x)

    # calculate new parameters
    wŒ∏ = (vx' + mx*mx') .* mw
    mŒ∏ = inv(wŒ∏) * Diagonal(mx) * mw * my

    # create variational message
    Message(Multivariate, GaussianWeightedMeanPrecision, xi=wŒ∏*mŒ∏, w=wŒ∏)

end


function ruleVariationalARFBIn3PPPN(marg_y::ProbabilityDistribution{Multivariate}, 
                                    marg_x::ProbabilityDistribution{Multivariate}, 
                                    marg_Œ∏::ProbabilityDistribution{Multivariate}, 
                                    marg_w::Nothing)

    # calculate required means
    my = unsafeMean(marg_y)
    mx = unsafeMean(marg_x)
    mŒ∏ = unsafeMean(marg_Œ∏)

    # calculate required variances
    vy = unsafeCov(marg_y)
    vx = unsafeCov(marg_x)
    vŒ∏ = unsafeCov(marg_Œ∏)

    # calculate new parameters
    v = vy + my*my' - (mŒ∏ .* mx)*my' - my*(mx .* mŒ∏)' + Diagonal(mŒ∏)*vx*Diagonal(mŒ∏) + Diagonal(mx)*vŒ∏*Diagonal(mx)  + (mŒ∏ .* mx)*(mŒ∏ .* mx)' + vŒ∏.*vx
    nu = size(v,1) + 2 

    # create variational message
    Message(MatrixVariate, Wishart, v=inv(v), nu=nu)

end

@naiveVariationalRule(:node_type     => AutoregressiveFilterbank,
                      :outbound_type => Message{GaussianWeightedMeanPrecision},
                      :inbound_types => (Nothing, ProbabilityDistribution, ProbabilityDistribution, ProbabilityDistribution),
                      :name          => VariationalARFBOutNPPP)

@naiveVariationalRule(:node_type     => AutoregressiveFilterbank,
                      :outbound_type => Message{GaussianWeightedMeanPrecision},
                      :inbound_types => (ProbabilityDistribution, Nothing, ProbabilityDistribution, ProbabilityDistribution),
                      :name          => VariationalARFBIn1PNPP)

@naiveVariationalRule(:node_type     => AutoregressiveFilterbank,
                      :outbound_type => Message{GaussianWeightedMeanPrecision},
                      :inbound_types => (ProbabilityDistribution, ProbabilityDistribution, Nothing, ProbabilityDistribution),
                      :name          => VariationalARFBIn2PPNP)

@naiveVariationalRule(:node_type     => AutoregressiveFilterbank,
                      :outbound_type => Message{Wishart},
                      :inbound_types => (ProbabilityDistribution, ProbabilityDistribution, ProbabilityDistribution, Nothing),
                      :name          => VariationalARFBIn3PPPN)
